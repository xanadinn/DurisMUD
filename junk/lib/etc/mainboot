#!/bin/tcsh -f
unlimit coredumpsize
if (-f src/duris/dms_new) then
  mv src/duris/dms_new dms
endif
while (1)
   if (-d logs/log) then
      if (-e DMS.ERR) then
         mv DMS.ERR logs/log
      endif
      set lgname=`date +%b%d-%H%M`
      mv logs/log logs/old-logs/$lgname
      if (-e logs/old-logs/last-log) then
           rm logs/old-logs/last-log
      endif
      ln -s $lgname logs/old-logs/last-log
      unset lgname
   endif
   mkdir logs/log

   if (-f "areas/tworld.wld") then
     mv areas/tworld.wld areas/world.wld
   endif
   if (-f "areas/tworld.mob") then
   	 mv areas/tworld.mob areas/world.mob
   endif
   if (-f "areas/tworld.obj") then
   	 mv areas/tworld.obj areas/world.obj
   endif
   if (-f "areas/tworld.zon") then
   	 mv areas/tworld.zon areas/world.zon
   endif
   if (-f "areas/tworld.shp") then
   	 mv areas/tworld.shp areas/world.shp
   endif
   if (-f "areas/tworld.qst") then
   	 mv areas/tworld.qst areas/world.qst
   endif

   if (-f "core2") then
     mv core2 core3
     rm -f core2
   endif
   if (-f "core1") then
     mv core1 core2
     rm -f core1
   endif
   if (-f "core") then
      mv core core1
   endif

   dms 6666 >>& DMS.ERR

   switch($status)
   case 0:
      echo -n 'Shutdown! ' >> DMS.ERR
      sleep 300
      breaksw
   case 52:
      echo -n 'Reboot! ' >> DMS.ERR
      breaksw
   case 53:
      sleep 10
      if (-f src/duris/dms_new) then
         echo -n 'Copyover from dms_new to dms! ' >> DMS.ERR
         mv dms dms_old
         cp src/duris/dms_new dms
         rm -f src/duris/dms_new
      else
         echo -n 'Reboot! ' >> DMS.ERR
      endif
      breaksw
   default:
      echo -n 'Mud crashed again, eh? ' >> DMS.ERR
      if (-f src/duris/dms_copy) then
        mv dms dms_old
        mv src/duris/dms_copy dms
      endif
      sleep 10
      breaksw
   endsw
   date >> DMS.ERR
end

